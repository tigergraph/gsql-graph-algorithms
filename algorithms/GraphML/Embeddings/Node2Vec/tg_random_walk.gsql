CREATE QUERY tg_random_walk(int step = 8, int path_size = 4, string filepath = "/home/tigergraph/path.csv", set<string> edge_types, int sample_num) {

 FILE f(filepath);
 ListAccum<ListAccum<vertex>> @recv_seque;
 ListAccum<ListAccum<vertex>> @send_seque;

 start (ANY) = {ANY};
 start = select s from start:s
        POST-ACCUM s.@send_seque += [s];

 while true limit step do
   tmp = select t from start:s-(edge_types:e)->:t
         SAMPLE sample_num EDGE  when s.outdegree() >= 1
         accum t.@recv_seque += s.@send_seque
         POST-ACCUM
           t.@send_seque.clear(),
           foreach path in t.@recv_seque do
             case when path.size() == path_size - 1 THEN
               f.println(path + [t])
             else
               t.@send_seque += path + [t]
             end
           end,
           t.@send_seque += [t],
           t.@recv_seque.clear();
 end;

}
